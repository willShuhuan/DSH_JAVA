apply plugin: 'com.android.application'
apply from: this.rootProject.file('versionInfo.gradle')
apply plugin: 'com.dsh.gradle.study'
import com.dsh.gradle.study.VersionInfoTask

android {
    //config.gradle 扩展属性 ext
    // compileSdkVersion this.rootProject.ext.compileSdkVersion
    // 也可以直接调用
    compileSdkVersion this.compileSdkVersion
    buildToolsVersion this.rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        applicationId applicationID//使用gradle.properties 拓展属性
        minSdkVersion this.rootProject.ext.android["minSdkVersion"]
        targetSdkVersion this.rootProject.ext.android["targetSdkVersion"]
        versionCode this.rootProject.ext.android["versionCode"]
        versionName this.rootProject.ext.android["versionName"]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //指定jniLibs .so存放位置 libs(默认在src/main/jniLibs下)
    sourceSets{
        main {
            jniLibs.srcDirs=['libs']
        }
    }
    //    //资源分包
    //    sourceSets{
    //        main{
    //            res.srcDirs = ['res/main/res','res/main/res_player']
    //        }
    //    }

    buildTypes {

        debug {
            //自定义applicationId，appName及一些其他属性
            applicationIdSuffix applicationID_test//applicationId的后缀,相当于更改了applicationId,可以认为是一个新的应用
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [APP_NAME    : getAppName('debug')]
            //--------------------------------------------------------------------------------
            debuggable true //默认false,是否开启断点调试
            jniDebuggable true //默认false,是否开启jni的断点调试
            renderscriptDebuggable true //默认false,是否开启 renderscript 断点调试
            renderscriptOptimLevel 2 //renderscript的版本号
            minifyEnabled true //默认false,是否混淆的开关
            pseudoLocalesEnabled true //默认false,是否支持本地化整理
            versionNameSuffix '.10' //版本名的后缀
            zipAlignEnabled false //默认true,是否进行zip align优化
            buildConfigField "boolean", "IS_DEBUG", "true"//为BuildConfig类创建一个常量,类型为boolean,变量名为IS_DEBUG,值为true
            resValue "string", "name", "value" //在strings.xml中创建一个名为name,值为value的一个字符串资源

        }

        release {
            applicationIdSuffix applicationID_release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [APP_NAME    : getAppName('release')]
        }

    }

    //配置签名方式，这里配置会覆盖defaultConfig中配置的签名方式
    signingConfigs {
        release {
            storeFile file("../appsign")
            storePassword "123456"
            keyAlias "dsh"
            keyPassword "123456"
        }
        buildTypes {
            debug {
                signingConfig signingConfigs.release
            }

            release {
                signingConfig signingConfigs.release
            }
        }
    }



    //多渠道打包,三方统计
    flavorDimensions "default"
    productFlavors{
        DshJava{}
        _xiaomi{}
        _huawei{}
        _oppo{}
        _vivo{}
        //        productFlavors.all { flavor ->
        //            flavor.manifestPlaceholders = [APP_DOWNLOAD_CHANNEL: name]
        //        }
    }

    //apk输出命名规范
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def releaseApkName = "app-v" +
                    "${defaultConfig.versionName}-" +
                    "${buildTime()}-"+
                    "${variant.buildType.name}.apk"
            outputFileName = new File(releaseApkName)
        }
    }

}

def getAppName(String type) {
    return new String("JavaStudy_${type}")
}

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyy_MM_dd')
    return formattedDate
}


//为应用程序添加第三方依赖
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
}

//为自定义插件传递参数
dshJavaVersionInfo{
    versionName = rootProject.ext.android.versionName
    versionCode = rootProject.ext.android.versionCode
    versionInfo = 'App的第6个版本，我有自定义插件功能了哦'
    fileName = 'releases.xml'
}
//自定义插件功能task执行
task updateVersionInfo(type:VersionInfoTask)
